# Lab 1 â€“ Criando um Resource Group com Terraform (Azure)

## ðŸŽ¯ Objetivo
Criar um Resource Group na Azure usando Terraform, passo a passo.

## ðŸ“ Estrutura de DiretÃ³rios
```bash
mkdir -p ~/labs/terraform/lab1-resource-group
cd ~/labs/terraform/lab1-resource-group
```

## ðŸ“„ Passo 1 â€“ Criar o arquivo de provider
```bash
vi provider.tf
```
ConteÃºdo:
```t
provider "azurerm" {
  features {}
}

terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = ">= 3.0"
    }
  }
  required_version = ">= 1.0"
}
```

## ðŸ“„ Passo 2 â€“ Criar o arquivo de configuraÃ§Ã£o principal
```bash
vi main.tf
```
ConteÃºdo:
```hcl
resource "azurerm_resource_group" "rg" {
  name     = "devops-rg"
  location = "East US"
}
```

## âœ… Passo 3 â€“ Inicializar o Terraform
```bash
terraform init
```

## ðŸ” Passo 4 â€“ Validar e revisar o plano
```bash
terraform validate
terraform plan
```

## ðŸš€ Passo 5 â€“ Aplicar o cÃ³digo e provisionar
```bash
terraform apply
```
Confirme com `yes` quando solicitado.

## ðŸ§¼ Passo 6 â€“ Destruir os recursos (opcional)
```bash
terraform destroy
```

---

# Lab 2 â€“ Criando uma VM Ubuntu com VariÃ¡veis e Data Source (Azure)

## ðŸŽ¯ Objetivo
Criar uma mÃ¡quina virtual Ubuntu na Azure usando Terraform com variÃ¡veis e buscando o resource group existente via `data`.

## ðŸ“ Estrutura de DiretÃ³rios
```bash
mkdir -p ~/labs/terraform/lab2-vm-variaveis
cd ~/labs/terraform/lab2-vm-variaveis
```

## ðŸ“„ Passo 1 â€“ Criar o arquivo de provider
```bash
vi provider.tf
```
ConteÃºdo:
```t
provider "azurerm" {
  features {}
}

terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = ">= 3.0"
    }
  }
  required_version = ">= 1.0"
}
```

## ðŸ“„ Passo 2 â€“ Definir variÃ¡veis
```bash
vi variables.tf
```

ConteÃºdo:
```t
variable "location" {
  default = "East US"
}

variable "resource_group_name" {
  default = "devops-rg"
}

variable "admin_username" {
  default = "azureuser"
}

variable "admin_password" {
  default = "SenhaForte123!@#"
}
```

## ðŸ“„ Passo 3 â€“ Criar o arquivo de configuraÃ§Ã£o principal
```bash
vi main.tf
```
ConteÃºdo:
```t
data "azurerm_resource_group" "rg" {
  name = var.resource_group_name
}

resource "azurerm_public_ip" "public_ip" {
  name                = "devops-public-ip"
  location            = var.location
  resource_group_name = data.azurerm_resource_group.rg.name
  allocation_method   = "Static"
  sku                 = "Basic"
}

resource "azurerm_virtual_network" "vnet" {
  name                = "devops-vnet"
  address_space       = ["10.0.0.0/16"]
  location            = var.location
  resource_group_name = data.azurerm_resource_group.rg.name
}

resource "azurerm_subnet" "subnet" {
  name                 = "devops-subnet"
  resource_group_name  = data.azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.1.0/24"]
}

resource "azurerm_network_interface" "nic" {
  name                = "devops-nic"
  location            = var.location
  resource_group_name = data.azurerm_resource_group.rg.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.public_ip.id
  }
}

resource "azurerm_linux_virtual_machine" "vm" {
  name                = "devops-vm"
  resource_group_name = data.azurerm_resource_group.rg.name
  location            = var.location
  size                = "Standard_B1s"
  admin_username      = var.admin_username
  admin_password      = var.admin_password
  disable_password_authentication = false
  network_interface_ids = [
    azurerm_network_interface.nic.id
  ]
  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }
  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-focal"
    sku       = "20_04-lts"
    version   = "latest"
  }
}

resource "azurerm_network_security_group" "nsg" {
  name                = "devops-nsg"
  location            = var.location
  resource_group_name = data.azurerm_resource_group.rg.name

  security_rule {
    name                       = "Allow-SSH"
    priority                   = 1001
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
}

resource "azurerm_network_interface_security_group_association" "nic_nsg" {
  network_interface_id      = azurerm_network_interface.nic.id
  network_security_group_id = azurerm_network_security_group.nsg.id
}
```

## âœ… Passo 4 â€“ Inicializar o Terraform
```bash
terraform init
```

## ðŸ” Passo 5 â€“ Validar e revisar o plano
```bash
terraform validate
terraform plan
```

## ðŸš€ Passo 6 â€“ Aplicar o cÃ³digo e provisionar
```bash
terraform apply
```
Confirme com `yes` quando solicitado.

## ðŸ§¼ Passo 7 â€“ Destruir os recursos (opcional)
```bash
terraform destroy
```

---

# Lab 01 â€“ Criando um AKS com Terraform (Camada Gratuita)

## O Que Vamos Fazer

- Criar um cluster AKS usando Terraform
- Usar a camada gratuita do Azure (Free Tier)
- Autenticar via Azure CLI
- Aplicar a infraestrutura com comandos passo a passo
- Validar e acessar o cluster com `kubectl`

---

## Estrutura de DiretÃ³rios

```bash
aks-free-tier/
â”œâ”€â”€ main.tf
â”œâ”€â”€ variables.tf
â”œâ”€â”€ outputs.tf
â”œâ”€â”€ provider.tf
â”œâ”€â”€ keda.tf
â”œâ”€â”€ helm-ingress.tf
```

```bash
mkdir -p ~/labs/terraform/aks-free-tier
cd ~/labs/terraform/aks-free-tier

touch main.tf variables.tf outputs.tf provider.tf keda.tf helm-ingress.tf
```

---

1. Use the curl command to download the kubectl binary:

```bash
curl -LO "https://dl.k8s.io/release/$(curl -Ls https://dl.k8s.io/release/stable.txt)/bin/linux/amd64/kubectl"
```
This command fetches the latest stable version of kubectl and downloads the appropriate binary for the AMD64 architecture.

Give execute permission to the binary:
```bash
chmod +x kubectl
```
Move the kubectl binary to a directory in your PATH. For example, you can move it to /usr/local/bin/:
```bash
sudo mv kubectl /usr/local/bin/
```
Verify that the installation was successful by running the following command:
```bash
kubectl version --client
```

### Download the Helm installation script:

1. Download a file
```bash
curl -fsSL -o get_helm.sh https://raw.githubusercontent.com/helm/helm/master/scripts/get-helm-3
```

2. Make the script executable:

```bash
chmod +x get_helm.sh
```
3. Run the installation script to install Helm:

```bash
./get_helm.sh
```
4. Verify the installation by checking the Helm version:

```bash
helm version
```

### How to install AzCli

```bash
sudo apt install azure-cli
```

### Atualizar pacotes
```bash
sudo apt update && sudo apt upgrade -y
```
###  Instalar dependÃªncias
```bash
sudo apt install -y gnupg software-properties-common curl
```

###  Adicionar chave GPG da HashiCorp
```bash
curl -fsSL https://apt.releases.hashicorp.com/gpg | sudo gpg --dearmor -o /usr/share/keyrings/hashicorp-archive-keyring.gpg
```

###  Adicionar repositÃ³rio oficial
```bash
echo "deb [signed-by=/usr/share/keyrings/hashicorp-archive-keyring.gpg] https://apt.releases.hashicorp.com $(lsb_release -cs) main" | sudo tee /etc/apt/sources.list.d/hashicorp.list
```

###  Atualizar repositÃ³rios e instalar Terraform
```bash
sudo apt update
sudo apt install -y terraform
```

# Verificar instalaÃ§Ã£o
```bash
terraform -v
```

## provider.tf

```t
provider "azurerm" {
  features {}
}

terraform {
  required_version = ">= 1.0.0"

  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 3.0"
    }
    kubernetes = {
      source = "hashicorp/kubernetes"
      version = "~> 2.0"
    }
    helm = {
      source = "hashicorp/helm"
      version = "~> 2.0"
    }
  }
}

provider "kubernetes" {
  host                   = azurerm_kubernetes_cluster.aks.kube_config[0].host
  client_certificate     = base64decode(azurerm_kubernetes_cluster.aks.kube_config[0].client_certificate)
  client_key             = base64decode(azurerm_kubernetes_cluster.aks.kube_config[0].client_key)
  cluster_ca_certificate = base64decode(azurerm_kubernetes_cluster.aks.kube_config[0].cluster_ca_certificate)
}

provider "helm" {
  kubernetes {
    host                   = azurerm_kubernetes_cluster.aks.kube_config[0].host
    client_certificate     = base64decode(azurerm_kubernetes_cluster.aks.kube_config[0].client_certificate)
    client_key             = base64decode(azurerm_kubernetes_cluster.aks.kube_config[0].client_key)
    cluster_ca_certificate = base64decode(azurerm_kubernetes_cluster.aks.kube_config[0].cluster_ca_certificate)
  }
}
```

---

## variables.tf

```hcl
variable "resource_group_name" {
  default = "rg-aks-free"
}

variable "location" {
  default = "eastus"
}

variable "aks_cluster_name" {
  default = "aks-free-tier"
}
```

---

## main.tf

```t
data "azurerm_kubernetes_cluster" "aks_data" {
  name                = azurerm_kubernetes_cluster.aks.name
  resource_group_name = azurerm_kubernetes_cluster.aks.resource_group_name
}

resource "azurerm_resource_group" "rg" {
  name     = var.resource_group_name
  location = var.location
}

resource "azurerm_kubernetes_cluster" "aks" {
  name                = var.aks_cluster_name
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  dns_prefix          = "aksfree"

  default_node_pool {
    name       = "default"
    node_count = 2
    vm_size    = "Standard_B2s"  # ideal para camada gratuita
  }

  identity {
    type = "SystemAssigned"
  }

  tags = {
    Environment = "Dev"
  }
}

resource "azurerm_public_ip" "ingress_ip" {
  name                = "myAKSPublicIPForIngress"
  location            = azurerm_resource_group.rg.location
  resource_group_name = data.azurerm_kubernetes_cluster.aks_data.node_resource_group
  allocation_method   = "Static"
  sku                 = "Standard"
}
```

---


## helm_ingress.tf

```t
resource "kubernetes_namespace" "ingress" {
  metadata {
    name = "ingress-nginx"
  }
}

resource "helm_release" "nginx_ingress" {
  name       = "ingress-nginx"
  namespace  = kubernetes_namespace.ingress.metadata[0].name
  repository = "https://kubernetes.github.io/ingress-nginx"
  chart      = "ingress-nginx"
  version    = "4.9.1"

  values = [
    <<EOF
controller:
  replicaCount: 2
  nodeSelector:
    beta.kubernetes.io/os: linux
  service:
    type: LoadBalancer
    externalTrafficPolicy: Local
    loadBalancerIP: "${azurerm_public_ip.ingress_ip.ip_address}"

defaultBackend:
  nodeSelector:
    beta.kubernetes.io/os: linux
EOF
  ]

  depends_on = [azurerm_public_ip.ingress_ip]
}

```

## helm_keda.tf

```t
resource "kubernetes_namespace" "keda" {
  metadata {
    name = "keda"
  }
}

resource "helm_release" "keda" {
  name       = "keda"
  namespace  = kubernetes_namespace.keda.metadata[0].name
  repository = "https://kedacore.github.io/charts"
  chart      = "keda"
  version    = "2.13.2"

  values = [
    <<EOF
replicaCount: 1
EOF
  ]

  depends_on = [kubernetes_namespace.keda]
}

```

## outputs.tf

```t

output "next_steps" {
  value = <<EOT
ðŸŽ‰ Cluster AKS criado com sucesso!

ðŸ‘‰ Para acessar seu cluster, execute o seguinte comando:

az aks get-credentials --resource-group ${var.resource_group_name} --name ${var.aks_cluster_name}

ðŸš€ Ingress Controller instalado com sucesso!

âœ… Acesse o IP fixo criado:
http://${azurerm_public_ip.ingress_ip.ip_address}

ðŸ“„ VocÃª verÃ¡ uma mensagem "404 Not Found" do NGINX â€” isso Ã© esperado!

âž¡ï¸ Prossiga para o Lab 03 para criar uma aplicaÃ§Ã£o + Ingress Resource.

EOT
}

output "resource_group_name" {
  value = var.resource_group_name
}

output "aks_cluster_name" {
  value = var.aks_cluster_name
}
```

---

## Passo a Passo para Rodar

### PrÃ©-requisitos

- Instalar [Azure CLI](https://learn.microsoft.com/cli/azure/install-azure-cli)
- Instalar [Terraform](https://developer.hashicorp.com/terraform/downloads)
- Ter uma conta Azure com crÃ©ditos ou Free Tier

---

### 1. Login no Azure

```bash
az login
```

---

### 2. Inicializar o Terraform

```bash
terraform init
```

---

### 3. Visualizar o plano de execuÃ§Ã£o

```bash
terraform plan
```

---

### 4. Aplicar a infraestrutura

```bash
terraform apply -auto-approve
```

---

### 5. Acessar o cluster AKS

```bash
az aks get-credentials --resource-group rg-aks-free --name aks-free-tier
kubectl get nodes
```

# Lab 2 - Criando seu primeiro Pod no Kubernetes (porta 8081)

## Objetivo

Criar manualmente um Pod com a imagem `iesodias/java-api:latest`, rodando na porta `8081`, e interagir com ele via `kubectl`.

---
